plugins {
    id "java"
    id "jacoco"
    id "org.sonarqube" version "5.1.0.4882"
    id "info.solidsoft.pitest" version "1.15.0"
    id "org.springframework.boot" version "3.3.5"
    id 'io.spring.dependency-management' version "1.1.6"
    id "org.jsonschema2dataclass" version "6.0.0"
}

group = "uk.gov.justice.laa.crime"

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

def versions = [
        pitest                      : "1.16.1",
        springdocVersion            : "2.6.0",
        crimeCommonsClasses         : "4.1.0",
        commonsModSchemas           : "1.26.1",
        springCloudStubRunnerVersion: "4.1.4",
        sentryVersion               : "7.12.1",
        resilience4jVersion         : "2.2.0",
        postgresqlVersion           : "42.7.2",
        commonsLang3Version         : "3.15.0",
        tomcatEmbedCoreVersion      : "10.1.34"
]

configurations {
    configureEach {
        exclude group: 'junit', module: 'junit'
    }
}

repositories {
    mavenCentral()
}

dependencies {

    implementation platform("io.sentry:sentry-bom:$versions.sentryVersion")

    implementation "io.sentry:sentry-spring-boot-starter-jakarta"
    implementation "io.sentry:sentry-logback"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-webflux"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-client"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:$versions.springdocVersion"
    implementation "org.apache.commons:commons-lang3:$versions.commonsLang3Version"
    implementation "io.github.resilience4j:resilience4j-spring-boot3:$versions.resilience4jVersion"

    implementation "org.postgresql:postgresql:$versions.postgresqlVersion"
    implementation "org.liquibase:liquibase-core"

    implementation "io.micrometer:micrometer-registry-prometheus"
    implementation "io.micrometer:micrometer-tracing"
    implementation "io.micrometer:micrometer-tracing-bridge-brave"

    implementation "uk.gov.justice.service.laa-crime:crime-commons-classes:$versions.crimeCommonsClasses"
    implementation "uk.gov.justice.service.laa-crime:crime-commons-mod-schemas:$versions.commonsModSchemas"

    implementation "org.apache.tomcat.embed:tomcat-embed-core:$versions.tomcatEmbedCoreVersion"

    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    testImplementation "com.h2database:h2"
    testImplementation "org.pitest:pitest:$versions.pitest"
    testImplementation "org.springframework.security:spring-security-test"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.cloud:spring-cloud-starter-contract-stub-runner:$versions.springCloudStubRunnerVersion"
}

jacoco {
    toolVersion = "0.8.8"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "**/entity/**",
                    "**/dto/**",
                    "**/config/**"
            ])
        }))
    }
}

pitest {
    targetClasses = [
            "uk.gov.justice.laa.crime.hardship.*",
            "uk.gov.justice.laa.crime.hardship.controller.*",
            "uk.gov.justice.laa.crime.hardship.validation.*"
    ]
    excludedClasses = [
            "uk.gov.justice.laa.crime.hardship.dto.*",
            "uk.gov.justice.laa.crime.hardship.mapper.*",
            "uk.gov.justice.laa.crime.hardship.CrimeHardshipApplication"
    ]
    threads = 5
    outputFormats = ["XML", "HTML"]
    timestampedReports = false
    mutationThreshold = 60
}

tasks["sonar"].dependsOn test

sonar {
    properties {
        property "sonar.exclusions", "**/entity/**.java , **/model/**.java , **/dto/**.java , **/config/**.java, **/jms/**.java,**/exception/**.java,**/handler/**.java"
        property "sonar.coverage.exclusions", "**/CrimeHardshipApplication.java"
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.projectKey", "ministryofjustice_laa-crime-hardship"
        property "sonar.organization", "ministryofjustice"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.projectName", "Crime Apps :: crime-hardship"
        property "sonar.pitest.mode", "reuseReport"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    }
}
