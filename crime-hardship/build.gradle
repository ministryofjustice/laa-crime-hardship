plugins {
    id "java"
    id "jacoco"
    id "org.sonarqube" version "6.1.0.5360"
    id "info.solidsoft.pitest" version "1.15.0"
    id "org.springframework.boot" version "3.4.4"
    id 'io.spring.dependency-management' version "1.1.6"
}

group = "uk.gov.justice.laa.crime"

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

def versions = [
        pitest               : "1.16.1",
        springdoc            : "2.6.0",
        crimeCommonsClasses  : "4.7.1",
        commonsModSchemas    : "1.26.1",
        sentry               : "7.12.1",
        resilience4j         : "2.2.0",
        postgreSQL           : "42.7.2",
        commonsLang          : "3.15.0",
        wiremock             : "3.10.0"
]

configurations {
    configureEach {
        exclude group: 'junit', module: 'junit'
    }
}

repositories {
    mavenCentral()
}

dependencies {

    implementation platform("io.sentry:sentry-bom:$versions.sentry")
    implementation "io.sentry:sentry-spring-boot-starter-jakarta"
    implementation "io.sentry:sentry-logback"

    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-webflux"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-client"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"

    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:$versions.springdoc"
    implementation "org.apache.commons:commons-lang3:$versions.commonsLang"

    implementation "io.github.resilience4j:resilience4j-reactor:$versions.resilience4j"
    implementation "io.github.resilience4j:resilience4j-spring-boot3:$versions.resilience4j"

    implementation "org.liquibase:liquibase-core"
    implementation "org.postgresql:postgresql:$versions.postgreSQL"

    implementation "io.micrometer:micrometer-tracing"
    implementation "io.micrometer:micrometer-registry-prometheus"
    implementation "io.micrometer:micrometer-tracing-bridge-brave"

    implementation "uk.gov.justice.service.laa-crime:crime-commons-classes:$versions.crimeCommonsClasses"
    implementation "uk.gov.justice.service.laa-crime:crime-commons-mod-schemas:$versions.commonsModSchemas"

    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    testImplementation "com.h2database:h2"
    testImplementation "org.pitest:pitest:$versions.pitest"
    testImplementation "org.springframework.security:spring-security-test"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.wiremock.integrations:wiremock-spring-boot:${versions.wiremock}"


    testCompileOnly "org.projectlombok:lombok"
    testAnnotationProcessor "org.projectlombok:lombok"
}

jacoco {
    toolVersion = "0.8.8"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "**/entity/**",
                    "**/dto/**",
                    "**/config/**"
            ])
        }))
    }
}

pitest {
    targetClasses = [
            "uk.gov.justice.laa.crime.hardship.*",
            "uk.gov.justice.laa.crime.hardship.controller.*",
            "uk.gov.justice.laa.crime.hardship.validation.*"
    ]
    excludedClasses = [
            "uk.gov.justice.laa.crime.hardship.dto.*",
            "uk.gov.justice.laa.crime.hardship.mapper.*",
            "uk.gov.justice.laa.crime.hardship.CrimeHardshipApplication"
    ]
    threads = 5
    outputFormats = ["XML", "HTML"]
    timestampedReports = false
    mutationThreshold = 60
}


def isCiBuild = System.getenv("SONAR_TOKEN") != null

if (isCiBuild) {
    tasks.named("sonar") {
        dependsOn "test"
    }
}

sonar {
    skipProject = !isCiBuild
    properties {
        property "sonar.exclusions", "**/entity/**.java , **/model/**.java , **/dto/**.java , **/config/**.java, **/jms/**.java,**/exception/**.java,**/handler/**.java"
        property "sonar.coverage.exclusions", "**/CrimeHardshipApplication.java"
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.projectKey", "ministryofjustice_laa-crime-hardship"
        property "sonar.organization", "ministryofjustice"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.projectName", "Crime Apps :: crime-hardship"
        property "sonar.pitest.mode", "reuseReport"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    }
}
